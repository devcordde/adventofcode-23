extern exit, mmap, putlong, findnotnum, atol, newline

section .text

%define curr rbx
%define eof [rsp + 0]
%define accumulator [rsp + 8]
%define springPtr rbp
%define runPtr r12
%define OPERATIONAL 0b00000001
%define DAMAGED     0b00000010
%define mainSequenceEndPtr r13
%define mainSequencePtr r14

global _start:function
_start:
  mov rdi, [rsp + 16]
  call mmap

  sub rsp, 2 * 8

  mov curr, rax
  add rax, rdx
  mov eof, rax

.lineLoop:
  mov QWORD accumulator, 0

  mov springPtr, springs
  mov runPtr, runs

  mov rdi, memoization
  mov rax, -1
  mov rcx, 128 * 64 * 32
  rep stosq

.readSprings:
  mov al, [curr]
  cmp al, '.'
  jne .readSpringsNotDot
  mov BYTE [springPtr], OPERATIONAL
  inc springPtr
  inc curr
  jmp .readSprings
.readSpringsNotDot:
  cmp al, '#'
  jne .readSpringsNotHash
  mov BYTE [springPtr], DAMAGED
  inc springPtr
  inc curr
  jmp .readSprings
.readSpringsNotHash:
  cmp al, '?'
  jne .readSpringsNotQuestion
  mov BYTE [springPtr], OPERATIONAL | DAMAGED
  inc springPtr
  inc curr
  jmp .readSprings
.readSpringsNotQuestion:
  mov mainSequenceEndPtr, springPtr
  mov rcx, 4
.expandSprings:
  mov mainSequencePtr, springs + 1
  mov BYTE [springPtr], OPERATIONAL | DAMAGED
  inc springPtr
.expandSpringsOnce:
  mov al, [mainSequencePtr]
  mov [springPtr], al
  inc springPtr
  inc mainSequencePtr
  cmp mainSequencePtr, mainSequenceEndPtr
  jb .expandSpringsOnce
  loop .expandSprings
  mov BYTE [springPtr], 0

.readRuns:
  inc curr
  mov rdi, curr
  call findnotnum
  mov rdi, curr
  mov rsi, rax
  mov curr, rax
  call atol
  mov [runPtr], rax
  add runPtr, 8
  cmp BYTE [curr], `\n`
  jne .readRuns
  mov mainSequenceEndPtr, runPtr
  mov rcx, 4
.expandRuns:
  mov mainSequencePtr, runs
.expandRunsOnce:
  mov rax, [mainSequencePtr]
  mov [runPtr], rax
  add runPtr, 8
  add mainSequencePtr, 8
  cmp mainSequencePtr, mainSequenceEndPtr
  jb .expandRunsOnce
  loop .expandRuns
  mov QWORD [runPtr], 0
  inc curr
  mov rdi, 0
  mov rsi, 0
  mov rdx, 0
  call solutions
  add accumulator, rax
  cmp curr, eof
  jb .lineLoop
  mov rdi, accumulator
  call putlong
  call newline
  mov dil, 0
  call exit

%define spring rdi
%define run rsi
%define currentRun rdx
%define thisSpring [rsp + 0]
%define thisRun [rsp + 8]
%define thisCurrentRun [rsp + 16]

solutions:
  mov r8, spring
  shl r8, 11
  mov r9, run
  shl r9, 5
  mov rax, currentRun
  add rax, r9
  add rax, r8
  cmp QWORD [memoization + rax * 8], -1
  jne .memoized
  lea rax, [springs + spring]
  cmp rax, springPtr
  jne .continue
  lea rax, [runs + run * 8]
  cmp rax, runPtr
  jne .continue
  test currentRun, currentRun
  jnz .continue
  jmp .unmemoizedOne
.continue:
  sub rsp, 3 * 8
  mov thisSpring, spring
  mov thisRun, run
  mov thisCurrentRun, currentRun
  test currentRun, currentRun
  jnz .notZero
  test BYTE [springs + spring], OPERATIONAL
  jz .overrun
  inc spring
  mov currentRun, 0
  call solutions
  mov run, thisRun
  mov spring, thisSpring
  inc spring
  mov currentRun, [runs + run * 8]
  inc run
  push rax
  call solutions
  pop rdi
  add rax, rdi
  jmp .unmemoized
.overrun:
  mov rax, 0
  jmp .unmemoizedZero
.notZero:
  test BYTE [springs + spring], DAMAGED
  jz .underrun
  inc spring
  dec currentRun
  call solutions
  jmp .unmemoized
.underrun:
  mov rax, 0
  jmp .unmemoizedZero
.unmemoized:
  mov spring, thisSpring
  mov run, thisRun
  mov currentRun, thisCurrentRun
.unmemoizedZero:
  add rsp, 3 * 8
  shl spring, 11
  shl run, 5
  add currentRun, spring
  add currentRun, run
  mov [memoization + currentRun * 8], rax
  ret
.unmemoizedOne:
  mov rax, 1
  shl spring, 11
  shl run, 5
  add currentRun, spring
  add currentRun, run
  mov [memoization + currentRun * 8], rax
  ret
.memoized:
  mov rax, [memoization + rax * 8]
  ret

%undef spring
%undef run
%undef currentRun
%undef thisSpring
%undef thisRun
%undef thisCurrentRun

section .bss
springs: resb 128
runs: resq 64
memoization: resq 128 * 64 * 32